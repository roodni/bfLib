# Brainfuckライブラリ

一応汎用性がある
説明は雑

## 1byte整数入力
### 符号なし
空白終端
```Brainfuck
>,>++++++++[-<---->]<[>++++[-<---->]<<[->++++++++++<]>[-<+>],>++++++++[-<---->]<]
```
改行終端
```Brainfuck
>,----------[>++++++[-<------>]<--<[->++++++++++<]>[-<+>],----------]
```

10進数整数の入力を受け取り[0]に格納する。

||0|1|2|
|-|-|-|-|
|開始|0|0|0|
|終了|入力|<b>0</b>|0|
終了位置：[1]

[2]をコード長短縮のためだけに使っている。

### 符号付き
空白終端
```Brainfuck
>,>+++++++++[-<----->]+<[>-<[-<+>]<+++++++++++++>]>[->+<<<++++++++++++++++>>]<<[---------------->[-<++++++++++>]<[->+<],>>++++++++[-<<---->>]<<]>>+>[<-<[-<->]>>-]<[-<[-<+>]>]
```

改行終端
```Brainfuck
>,>+++++++++[-<----->]+<[[-<+>]>++++++[-<<+++++>>]<]>[->+<<<,---------->>]<<[>[-<++++++++++>]>++++++[-<<------>>]<<--[->+<],----------]>>+>[<-<[-<->]>>-]<[-<[-<+>]>]
```
符号付き10進数整数の入力を受け取り[0]に格納する。
終了位置：[2]
[0]~[3]使用

<details>
<summary>元のコード</summary>

```
in ,
in - 45
in_else +
in [
  # 負数でないとき
  in_else -
  in inc po
  po + (45 - 終端記号)
] in_else [
  # 負数のとき
  sign +
  in_else -
  { po + (48 - 終端記号) }
  あるいは、
  { po ,
    po - (終端記号) }
]
po [
  po - (48 - 終端記号)
  num inc*10 po
  po inc num
  po ,
  po - (終端記号)
]
sign_else +
sign [
  sign -
  sign_else -
  num dec ans
] sign_else [
  sign_else -
  num inc ans
]
```
| 0 | 1 | 2 | 3 |
| --- | --- | --- | --- |
| po | in | in_else | sign |
| ans | num | sign_else | |

符号付き、空白(32)終端
```Brainfuck
> ,
----- ----- -----
----- ----- -----
----- ----- -----
> +
< [
  > - <
  [- < + >]
  < +++++ +++++ +++ >
] > [
  -
  > + <
  << +++++ +++++ +++++ + >>
]

<< [
  ----- ----- ----- -
  > [- < +++++ +++++ >] <
  [- > + <]
  ,
  ----- ----- -----
  ----- ----- -----
  --
]
>> +
> [
  < - >
  << [- < - >] >>
  -
] < [
  -
  < [- < + >] >
]

```
</details>

## 除算(定数)
```Brainfuck
>>+[>(割る数だけ加算)>[-]<[->>>+<<<<<[[->>>>+<<<<]>>>>>-<-<+<<<]>>>>>[-<<[->+<]<[-]<-<<->>>>>>]<[-<<<<+>>>>]<<]<<<+>>]
```

[1]を定数で割り商と余りを求める。
1byte整数出力のために作ったコードで少し使い勝手が悪い。

||0|1|2|3|4|5|6|
|-|-|-|-|-|-|-|-|
|開始|0|割られる数|0|0|0|0|0|
|終了|商|余り|<b>0</b>|0|0|0|0|
終了位置：[2]

<details>
<summary>元のコード</summary>

```basic
p → 0
a ← (割られる数)
f ← 1
while f
  b ← (割る数)
  _b ← 0
  for b
    if a → _a1 (_a2)
      dec _a1
      inc _b
    else _a2
      inc _a1 ← _b
      b ← 0
      f ← 0
      dec p
    end a ← _a1
  next
  inc p
wend
```

|0|1|2|3|4|5|6|
|---|---|---|---|---|---|---|
|p|a|f|b|_b|_a1|_a2|


```Brainfuck
>> +
[
  > (割る数だけ加算)
  > [-]
  < [-
    >>>+<<<<< [[->>>>+<<<<] >>>>>-<<<<<
      >>>> -
      < +
      <<<
    ] >>>>> [-
      << [->+<]
      < [-]
      < -
      << -
      >>>>>>
    ] < [-<<<<+>>>>]
    <<
  ]
  <<< +
  >>
]
```
</details>

## 1byte整数出力
```Brainfuck
>>+<<[[->+<]>>-<<]>[-<+>]>[++++++[-<+++++++>]<-.[-]>]<<
[[->>>+<<<]>>>>+[>++++++++++>[-]<[->>>+<<<<<[[->>>>+<<<<]>>>>>-<-<+<<<]>>>>>[-<<[->+<]<[-]<-<<->>>>>>]<[-<<<<+>>>>]<<]<<<+>>]+++++++[-<+++++++>]<-<]>[.<<]
```

[0]の値を整数で出力する。出力された値は破壊される。
1行目はゼロ出力用オプション。省くと`0`のみが出力されなくなる。

終了位置：[1]

### 符号付き整数出力オプション
```brainfuck
>>>>>+<<<++++++++++++++++[-<++++++++>]<[->>+<<<[[->>+<<]>>>-<->>+<<<<]>>>[->>-<<<<[-]>>]<[-<<+>>]<]<[->>>>-<<<<]>>>>[-<<<<+>>>>]>[-<+++++++++[-<+++++>]<.[-]>>]<<<<<
```
先頭につけると出力が符号付きになる。
すなわち、[0]≧128のとき`-`を出力し[0] ← 256-[0]とする。

<details>
<summary>元のコード</summary>

符号処理
```basic
f ← 1
a ← 128
for a
  if n → _n1 (_n2)
    dec _n1
    inc r
  else _n2
    f ← 0
    a ← 0
  end n ← _n1
next
dec r ← n
n ← r

rem f=1なら'-'出力
```
|0|1|2|3|4|5|
|-|-|-|-|-|-|
|n|a|_n1|_n2|r|f|


```Brainfuck
>>>>> +
<<< +++++ +++++ +++++ +[- < +++++ +++ >]
< [ -
  >> + <<< [[- >> + <<] >>> - <<<
    >> - <<
    >>>> + <<<<
  ] >>> [ -
    >> - <<
    << [-] >>
  ] < [- << + >>] <
]

< [- >>>> - <<<<]
>>>> [- <<<< + >>>>]

> [ -
  < +++++ ++++[- < +++++ >] >
  << .[-] >>
]

<<<<<
```

出力
擬似コードは忘れてしまった。解読する気はおきない
```Brainfuck
[
  商を移動
  [->>>+<<<]
  10で割る
  >>>> +
  [
    > +++++ +++++
    > [-]
    < [-
      >>>+<<<<< [[->>>>+<<<<] >>>>>-<<<<<
        >>>> -
        < +
        <<<
      ] >>>>> [-
        << [->+<]
        < [-]
        < -
        << -
        >>>>>>
      ] < [-<<<<+>>>>]
      <<
    ]
    <<< +
    >>
  ]
  余りに48加算
  +++++ ++[-<+++++ ++>]
  < -
  <
]
出力
> [
  .
  <<
]
```

</details>

## 大小比較(非破壊)
```Brainfuck
[->>+>>+<<<<]>>[-<<+>>]+<[->>+>>>+<<[[->+<]>>-<-<]>>[-<<<<-<[->>+<<]>>>>>]<[-<+>]<<<<]>>[-<<+>>]>[-]
```

`[2] = ([0] >= [1])`
終了位置：[4]
[0]~[6]使用

<details>
<summary>元のコード</summary>

↓このコードクソ汚いので書き直したい
```brainfuck
[- >> + >> + <<<<]
>> [- << + >>]
+
< [- >> + <<
  >>>>> + << [[- > + <] >> - <<
    > - <
  ] >> [-
    <<<< - >>>>
    <<<<< [- >> + <<] >>>>>
  ] < [- < + >] <<<<
] >> [- << + >>]
> [-]
```
↓修正計画
```basic
ans ← 1
for a → _a
  if b → _b1 (_b2)
    dec _b1
    inc r
  else _b2
    ans ← 0
    inc _a ← a
  end b ← _b1
next a ← _a
inc b ← r
```
|0|1|2|3|4|5|6|
|-|-|-|-|-|-|-|
|b|a|ans|r|_a|_b1|_b2|
```Brainfuck
>> +
< [- >>> + <<<
  >>>>> + <<<<<< [[- >>>>> + <<<<<] >>>>>> - <<<<<<
    >>>>> - <<<<<
    >>> + <<<
  ] >>>>>> [-
    <<<< - >>>>
    <<<<< [- >>> + <<<] >>>>>
  ] < [- <<<<< + >>>>>] <<<<
] >>> [- <<< + >>>]
< [- <<< + >>>]
```
書きなおしたら長くなってしまった。没。
```brainfuck
>>+<[->>>+>>+<<<<<<[[->>>>>+<<<<<]>>>>>>-<-<<+<<<]>>>>>>[-<<<<-<[->>>+<<<]>>>>>]<[-<<<<<+>>>>>]<<<<]>>>[-<<<+>>>]<[-<<<+>>>]
```
ちなみにこちらは使用帯域は変わらず0~6で終了位置は[3]。
</details>

## 大小比較([0][1]破壊)
```brainfuck
>>+<[->>>+<<<<[[->>>+<<<]>>>>-<-<<<]>>>>[-<<-<[-]>>>]<[-<<<+>>>]<<]<[-]
```
終了位置：[0]
[0]~[4]使用

これ絶対もっと短縮できると思う(具体的に：ansを削れると思う)

<details>
<summary>元のコード</summary>

```basic
ans ← 1
for a
  if b → _b1 (_b2)
    dec _b1
  else _b2
    ans ← 0
    a ← 0
  end b ← _b1
next
b ← 0
```
|0|1|2|3|4|
|-|-|-|-|-|
|b|a|ans|_b1|_b2|
```Brainfuck
>> +
< [-
  >>> + <<<< [[- >>> + <<<] >>>> - <<<<
    >>> - <<<
  ] >>>> [-
    << - >>
    <<< [-] >>>
  ] < [- <<< + >>>] <<
]
<[-]
```
</details>

## 絶対値
```brainfuck
>>++++++++++++++++[-<++++++++>]<[->>+<<<[[->>+<<]>>>-<->>+<<<<]>>>[-<<[-]>>]<[-<<+>>]<]<[->>>>-<<<<]>>>>[-<<<<+>>>>]
```
[0]を[0]の絶対値で上書きする。

終了位置：[4]
[0]~[4]使用

<details>
<summary>元のコード</summary>

1byte整数出力の符号処理から不要な処理を省いたもの。
```brainfuck
>> +++++ +++++ +++++ +[- < +++++ +++ >]
< [ -
  >> + <<< [[- >> + <<] >>> - <<<
    >> - <<
    >>>> + <<<<
  ] >>> [ -
    << [-] >>
  ] < [- << + >>] <
]
< [- >>>> - <<<<]
>>>> [- <<<< + >>>>]
```
</details>

## 多倍長10進カウンタ
メモリ構成
ヘッダ | 一 | 一 | 一 | ...
--- | --- | --- | --- | ---
0 | carry | num | backup, その桁が使用されているかどうか

インクリメント
```brainfuck
>+[>>[-]---------<[->+<]>[<<->>++++++++++[-<+>]]<<[->>>+<<<]>>+>]<[<<<]
```
開始位置：ヘッダ
終了位置：ヘッダ

出力
```Brainfuck
>>>[>>>]<<<[+++++++[-<++++++>]<.<<]
```
開始位置：ヘッダ
終了位置：ヘッダ


<details>
<summary>元のコード</summary>

インクリメント
```brainfuck
> +[
  >> [-]----- ---- <<
  > [- > + <] <
  >> [
    << - >>
    +++++ +++++
    [- < + >]
  ] <<
  [
    -
    >>> + <<<
  ]
  >> + <<
  >>>
]
< [ <<< ]
```

出力
```Brainfuck
>>> [ >>> ]
<<< [
  +++++ ++[- < +++++ + > ]
  < . >
  <<<
]
```
</details>
